# aeonius grammar specification

grammar: ε
       | aeonius grammar
       | PYTHONCODE grammar

aeonius: BEGIN EOL code END

code: ε
    | assignment code

assignment: pattern '=' exp EOL
	   | DEF IDENTIFIER ':' EOL INDENT defbody UNDENT
          | DEF IDENTIFIER ':' pattern match
          | OP '(' operator ')' ':' EOL INDENT defbody UNDENT
          | OP '(' operator ')' ':' pattern match

defbody: multipatternmatch code

multipatternmatch: pattern match
                 | multipatternmatch pattern match

multicondmatch: exp match
              | multicondmatch '|' exp match
              | multicondmatch '|' match

match: RESULTARROW INDENT exp EOL code UNDENT
     | RESULTARROW EOL INDENT exp EOL code UNDENT
     | RIGHTARROW INDENT defbody UNDENT
     | RIGHTARROW EOL INDENT defbody UNDENT
     | '|' INDENT multicondmatch code UNDENT
     


primitive: INTEGER
         | FLOAT
         | STRING
         | FALSE
         | TRUE
         | NONE
         | '[' ']'
         | '{' '}'


exp: IDENTIFIER
   | primitive %prec PRIMITIVE
   | '(' tupleexp ')'
   | '[' iterexp ']'
   | '{' dictexp '}'
   | exp exp %prec FUNC         	       #function call
   | '(' exp ')'	                     #Wrapped in brackets
   | LAMBDA pattern ':' exp
   | exp IF exp ELSE exp
   | exp FOR pattern IN exp
   | exp FOR pattern IN exp IF exp
   | '{' IDENTIFIER ':' exp FOR pattern IN exp '}'
   | '{' IDENTIFIER ':' exp FOR pattern IN exp IF exp '}'
   | '(' operator ')'                     #operator function
#   | operator exp		              #unary operator call
   | exp OPIDENT exp
   | exp OPIDENT_L1 exp
   | exp OPIDENT_L2 exp
   | exp OPIDENT_L3 exp
   | exp OPIDENT_L4 exp
   | exp OPIDENT_L5 exp
   | exp OPIDENT_R1 exp
   | exp OPIDENT_R2 exp
   | exp OPIDENT_R3 exp
   | exp OPIDENT_R4 exp
   | exp OPIDENT_R5 exp
   | exp UNPACKITER exp
   | exp UNPACKDICT exp
   | exp '|' exp

operator: OPIDENT
        | OPIDENT_L1
        | OPIDENT_L2
        | OPIDENT_L3
        | OPIDENT_L4
        | OPIDENT_L5
        | OPIDENT_R1
        | OPIDENT_R2
        | OPIDENT_R3
        | OPIDENT_R4
        | OPIDENT_R5
        | UNPACKITER
        | UNPACKDICT
        | '|'


elemexp: exp
       | UNPACKITER exp

tupleexp: elemexp ','
        | nonsingletupleexp
        | nonsingletupleexp ','

nonsingletupleexp: elemexp ',' elemexp
                 | nonsingletupleexp ',' elemexp

iterexp: nonemptyiterexp
       | nonemptyiterexp ','

nonemptyiterexp: elemexp
	        | nonemptyiterexp ',' elemexp

dictexp: nonemptydictexp
       | nonemptydictexp ','
	 
nonemptydictexp: exp ':' exp
	        | nonemptydictexp ',' exp ':' exp
               | nonemptydictexp ',' UNPACKDICT exp



pattern: primitive %prec PRIMITIVE
       | '_'
       | IDENTIFIER
#       | nonemptyiterpattern             #TODO
       | '(' pattern ')'
       | '(' tuplepattern ')'
       | '[' iterpattern ']'
       | '{' dictpattern '}'

tuplepattern: pattern ','
            | pattern ',' UNPACKITER pattern
            | nonsingletuplepattern
            | nonsingletuplepattern ','
            | nonsingletuplepattern ',' UNPACKITER pattern

nonsingletuplepattern: pattern ',' pattern
                     | nonsingletuplepattern ',' pattern

iterpattern: nonemptyiterpattern
	    | nonemptyiterpattern ','
           | nonemptyiterpattern ',' UNPACKITER pattern

nonemptyiterpattern: pattern
		     | nonemptyiterpattern ',' pattern

dictpattern: nonemptydictpattern
	    | nonemptydictpattern ','

nonemptydictpattern: pattern ':' pattern
		     | nonemptydictpattern ',' pattern ':' pattern
                   | nonemptydictpattern ',' UNPACKDICT pattern